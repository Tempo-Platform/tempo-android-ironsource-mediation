plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.ironsource.adapters.custom.tempo'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionName = '1.8.1-rc.0'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'io.github.tempo-platform:tempo-android-sdk:1.7.2-rc.15'
    api 'com.unity3d.ads-mediation:mediation-sdk:[8.1, 9.0)'

//    // Other
//    implementation(name:'tempo-android-sdk-release', ext:'aar')
//    implementation ('com.stripe:payments-core:[17, 18]') {
//        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
//        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
//    }
//    implementation 'org.jetbrains.kotlin:kotlin-stdlib:[1.8, 1.9]'
//    implementation 'commons-net:commons-net:3.9.0'
//    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
//    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
//    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
//    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
//    implementation 'com.google.android.gms:play-services-location:18.0.0'
}

/* - - - - - Maven Central Publishing - - - - - */

// Required to satisfy Maven requirements
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

// Required to satisfy Maven requirements
task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    // Include README.md or skip entirely if javadoc fails
    from 'README.md'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'io.github.tempo-platform'
            artifactId = 'tempo-android-ironsource-adapter'
            version = android.defaultConfig.versionName

            afterEvaluate {
                from components.release
                artifact sourcesJar
                artifact javadocJar
            }

            pom {
                name = artifactId
                description = "Tempo Mediation Adapter for ironSource"
                url = 'https://github.com/Tempo-Platform/tempo-android-ironsource-mediation'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'development'
                        name = 'development'
                        email = 'development@tempoplatform.com'
                    }
                }

                scm {
                    connection = 'git@github.com:Tempo-Platform/tempo-android-ironsource-mediation.git'
                    developerConnection = 'git@github.com:Tempo-Platform/tempo-android-ironsource-mediation.git'
                    url = 'https://github.com/Tempo-Platform/tempo-android-ironsource-mediation'
                }
            }
        }
    }

    repositories {
        maven {
            name = "maven"
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}




