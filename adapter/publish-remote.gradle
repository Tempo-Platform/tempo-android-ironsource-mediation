// Add the plugins required to publish and sign the artifact.
apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    PUBLISH_GROUP_ID = 'io.github.tempo-platform'
    PUBLISH_VERSION = '1.7.4-rc.1'
    PUBLISH_ARTIFACT_ID = 'tempo-android-ironsource-adapter'
}

// Declare the sources to ensure the correct source files are packed with executable, compiled code.
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

// Provide the metadata of the library to release
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                // The coordinates of the library, being set from variables that we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                System.out.println("PUBLISH_GROUP_ID\t\t: " + groupId)
                System.out.println("PUBLISH_ARTIFACT_ID\t\t: " + artifactId)
                System.out.println("PUBLISH_VERSION\t\t\t: " + version)

                if (project.plugins.findPlugin("com.android.library")) {

                    from components.release
                    artifact androidSourcesJar

                    // Mostly self-explanatory metadata
                    pom {
                        name = artifactId
                        description = "Tempo Mediation Adapter for ironSource"
                        url = 'https://github.com/Tempo-Platform/tempo-android-ironsource-mediation'
                        licenses {
                            license {
                                name = 'MIT license'
                                url = 'https://opensource.org/licenses/MIT'
                            }
                        }
                        developers {
                            developer {
                                id = 'development'
                                name = 'development'
                                email = 'development@gmail.com'
                            }
                        }

                        // Version control info
                        scm {
                            connection = 'git@github.com:Tempo-Platform/tempo-android-ironsource-mediation.git'
                            developerConnection = 'git@github.com:Tempo-Platform/tempo-android-ironsource-mediation.git'
                            url = 'https://github.com/Tempo-Platform/tempo-android-ironsource-mediation'
                        }

                    }

                } else {
                    System.out.println("ERROR: 'com.android.library' not found.")
                }
            }
        }
    }
}

// Get the signing details from the root project and sign the artifact.
signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}